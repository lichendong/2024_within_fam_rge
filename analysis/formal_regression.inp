TITLE:    
        Causal rGE on differential parenting;

DATA:   
        FILE IS "c:\Repo\data_local\withinfamrGE_addh.csv";




VARIABLE:
        Names are AID FAM GPAIRID GID GFAM 
        pgs ANCESTRY sex age_1 age_3 
        white african native asian other hispanic 
        income par_ed par_age 
        par_sex par_marry 
        bad_temper pr_parent pr_child del_peer scho_con
        asb_1 asb_3 sub_1 sub_3 
        par_sub autonomy par_involv;

        usevariables are FAM
         pgs sex age_1 age_3
         pr_parent pr_child del_peer scho_con
         asb_1 sub_1 asb_3 sub_3
         pgs_m sex_m age_1_m age_3_m
         income_m par_ed_m;
                
        CLUSTER IS FAM;
        
        WITHIN ARE
         pgs sex age_1 age_3
         ;

        BETWEEN ARE 
        pgs_m sex_m age_1_m age_3_m
        income_m par_ed_m;
    

        Missing are .;


DEFINE:
        income = log(income + 1);
        pgs_m = CLUSTER_MEAN(pgs);
        sex_m = CLUSTER_MEAN(sex);
        age_1_m = CLUSTER_MEAN(age_1);
        age_3_m = CLUSTER_MEAN(age_3);
        income_m = CLUSTER_MEAN(income);
        par_ed_m = CLUSTER_MEAN(par_ed);
        CENTER pgs sex age_1(GROUPMEAN); 
        CENTER age_3(GROUPMEAN);

ANALYSIS:

        TYPE = TWOLEVEL RANDOM;
        ESTIMATOR = BAYES;
        !ESTIMATOR=WLSM;
        !BOOTSTRAP = 20000;
        PROC = 10;
        CHAINS = 10;
        FBITERATIONS= 20000;

MODEL:
        
        %WITHIN%
        
        pr_parent pr_child del_peer scho_con ON pgs sex age_1;
        asb_1 sub_1 ON pgs sex age_1;
        asb_3 sub_3 ON pgs sex age_3;
        [pgs sex age_1 age_3 @0];
        pgs sex age_1 age_3;
        pgs sex age_1 age_3 WITH pgs sex age_1 age_3;

        %BETWEEN%
        pr_parent pr_child del_peer scho_con asb_1 sub_1 asb_3 sub_3;
        pgs_m sex_m age_1_m age_3_m income_m par_ed_m;
        pr_parent pr_child  ON pgs_m sex_m age_1_m income_m par_ed_m;
        del_peer scho_con ON pgs_m sex_m age_1_m income_m par_ed_m;
        asb_1 sub_1 ON pgs_m sex_m age_1_m income_m par_ed_m;
        asb_3 sub_3 ON pgs_m sex_m age_3_m income_m par_ed_m;


        pgs_m sex_m WITH pgs_m sex_m age_1_m age_3_m income_m par_ed_m;
        age_1_m age_3_m WITH pgs_m sex_m age_1_m age_3_m income_m par_ed_m;
        income_m par_ed_m WITH pgs_m sex_m age_1_m age_3_m income_m par_ed_m;

MODEL CONSTRAINT:

OUTPUT: TECH1 TECH8 CINTERVAL;
PLOT: TYPE = PLOT2;

